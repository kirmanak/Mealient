plugins {
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.android.application'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
    // https://plugins.gradle.org/plugin/com.guardsquare.appsweep
    id "com.guardsquare.appsweep" version "1.0.0"
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "gq.kirmanak.mealient"
        minSdk 23
        targetSdk 32
        versionCode 11
        versionName "0.2.2"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        buildConfigField "Boolean", "DEBUG_PICASSO", "false"
        buildConfigField "Boolean", "LOG_NETWORK", "false"
    }

    signingConfigs {
        release {
            rootProject.file("keystore.properties").with { keystorePropertiesFile ->
                if (keystorePropertiesFile.canRead()) {
                    def keystoreProperties = new Properties()
                    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                    keyAlias keystoreProperties['keyAlias']
                    keyPassword keystoreProperties['keyPassword']
                    storeFile file(keystoreProperties['storeFile'])
                    storePassword keystoreProperties['storePassword']
                } else {
                    println 'Unable to read keystore.properties'
                }
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-rules-debug.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lint {
        disable 'ObsoleteLintCustomCheck', 'IconMissingDensityFolder'
        enable 'ConvertToWebp', 'DuplicateStrings', 'EasterEgg', 'ExpensiveAssertion', 'IconExpectedSize', 'ImplicitSamInstance', 'InvalidPackage', 'KotlinPropertyAccess', 'LambdaLast', 'MinSdkTooLow', 'NegativeMargin', 'NoHardKeywords', 'Registered', 'RequiredSize', 'UnknownNullness', 'WrongThreadInterprocedural'
    }

    namespace 'gq.kirmanak.mealient'

    packagingOptions {
        resources.excludes += "DebugProbesKt.bin"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
}

dependencies {
    // https://github.com/material-components/material-components-android
    implementation "com.google.android.material:material:1.6.0"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-runtime-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // https://developer.android.com/kotlin/ktx#core
    implementation "androidx.core:core-ktx:1.7.0"

    // https://developer.android.com/jetpack/androidx/releases/appcompat
    implementation "androidx.appcompat:appcompat:1.4.1"

    // https://developer.android.com/jetpack/androidx/releases/constraintlayout
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"

    // https://developer.android.com/jetpack/androidx/releases/swiperefreshlayout
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    def lifecycle_version = "2.4.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    kaptTest "com.google.dagger:hilt-android-compiler:$hilt_version"
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"

    // https://github.com/square/retrofit/tags
    implementation "com.squareup.retrofit2:retrofit:2.9.0"

    // https://github.com/JakeWharton/retrofit2-kotlinx-serialization-converter/tags
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0"

    // https://github.com/square/okhttp/tags
    implementation platform("com.squareup.okhttp3:okhttp-bom:4.9.3")
    implementation "com.squareup.okhttp3:okhttp"
    debugImplementation "com.squareup.okhttp3:logging-interceptor"

    // https://github.com/Kotlin/kotlinx.serialization/releases
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3"

    // https://github.com/JakeWharton/timber/releases
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // https://developer.android.com/jetpack/androidx/releases/paging
    def paging_version = "3.1.1"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    testImplementation "androidx.paging:paging-common-ktx:$paging_version"

    // https://developer.android.com/jetpack/androidx/releases/room
    def room_version = "2.4.2"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-paging:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // https://github.com/Kotlin/kotlinx-datetime/releases
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.3.3"

    // https://github.com/bumptech/glide/releases
    def glide_version = "4.13.2"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    implementation("com.github.bumptech.glide:recyclerview-integration:$glide_version") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // https://github.com/androidbroadcast/ViewBindingPropertyDelegate/releases
    implementation "com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.6"

    // https://developer.android.com/jetpack/androidx/releases/datastore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // https://developer.android.com/jetpack/androidx/releases/security
    implementation "androidx.security:security-crypto:1.0.0"

    // https://mvnrepository.com/artifact/com.google.firebase/firebase-bom?repo=google
    implementation platform("com.google.firebase:firebase-bom:30.0.2")
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-perf-ktx"

    // https://github.com/junit-team/junit4/releases
    testImplementation "junit:junit:4.13.2"

    // https://github.com/Kotlin/kotlinx.coroutines/releases
    def coroutines_version = "1.6.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // https://github.com/robolectric/robolectric/releases
    testImplementation "org.robolectric:robolectric:4.8.1"

    // https://developer.android.com/jetpack/androidx/releases/test
    testImplementation "androidx.test.ext:junit-ktx:1.1.3"

    // https://mvnrepository.com/artifact/com.google.truth/truth
    testImplementation "com.google.truth:truth:1.1.3"

    // https://mockk.io/
    testImplementation "io.mockk:mockk:1.12.4"

    // https://github.com/facebook/flipper/releases
    def flipper_version = "0.147.0"
    debugImplementation "com.facebook.flipper:flipper:$flipper_version"
    debugImplementation "com.facebook.flipper:flipper-leakcanary2-plugin:$flipper_version"
    debugImplementation "com.facebook.flipper:flipper-network-plugin:$flipper_version"

    // https://github.com/facebook/SoLoader/releases
    debugImplementation "com.facebook.soloader:soloader:0.10.3"

    // https://github.com/square/leakcanary/releases
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.9.1"

}